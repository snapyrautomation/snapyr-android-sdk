// See snapyrRootProject in snapyr/build.gradle for more info
ext {
    snapyrRootProject = project.findProject(":snapyr-root")
    if (snapyrRootProject == null) {
        snapyrRootProject = rootProject
    }
}

// When SDK is included in another project locally, ensures root Snapyr SDK `build.gradle` is processed before continuing. This is needed to ensure Snapyr root dependencies and `ext` values are available by the time this file's tasks run
if (snapyrRootProject != rootProject) {
    evaluationDependsOn(snapyrRootProject.path)
}

apply plugin: 'com.diffplug.gradle.spotless'

android {
    compileSdkVersion snapyrRootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion snapyrRootProject.ext.minSdkVersion
        targetSdkVersion snapyrRootProject.ext.targetSdkVersion
        versionCode snapyrRootProject.ext.versionCode
        versionName snapyrRootProject.ext.versionName
    }

    dexOptions {
        preDexLibraries snapyrRootProject.ext.preDexLibraries
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility snapyrRootProject.ext.sourceCompatibilityVersion
        targetCompatibility snapyrRootProject.ext.targetCompatibilityVersion
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        disable 'InvalidPackage', 'GradleCompatible'
    }

    testOptions {
        unitTests.all {
            systemProperty 'robolectric.dependency.repo.url', 'https://repo1.maven.org/maven2'
        }
    }
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat().aosp()
//        licenseHeader getLicenseText()
    }
    kotlin {
        target '**/*.kt'
        ktlint('0.37.2').userData([
                'android'        : 'true',
                'max_line_length': '120',
        ])
//        licenseHeader getLicenseText()
    }
    groovyGradle {
        target '*.gradle'
        greclipse()
    }
}

def getLicenseText() {
    def rawTextLines = snapyrRootProject.file('LICENSE.md').text.split('\n')
    def text = ""
    for (def line : rawTextLines) {
        if (line.trim().length() == 0) {
            text = text + '\n *' + line
        } else {
            text = text + '\n * ' + line
        }
    }
    return '/**' + text + '\n */'
}
